#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если Параметры.Свойство("ПараметрыКоманды") Тогда
		дз = ПолучитьИзВременногоХранилища(Параметры.ПараметрыКоманды.АдресДереваСтруктурыМодулей);
		Для каждого СтрМодуль Из дз.Строки Цикл
			Если СтрМодуль.Выбрана Тогда
				ВыполнитьАнализМодуля(СтрМодуль);
				Прервать;
			КонецЕсли;
		КонецЦикла;
	Иначе
		ВызватьИсключение "В форму должен быть передан параметр ПараметрыКоманды.АдресДереваСтруктурыМодулей";
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ВыполнитьАнализМодуля(СтрМодуль)
	ТекстМодуля = Обработки.ОМ_Компонент_ГенерацияТекстовМодулей.ТекстМодуля(СтрМодуль);
	
	ПараметрыАнализа = Новый Структура;
	ПараметрыАнализа.Вставить("Модуль", СтрМодуль.Содержимое.Имя);
	ПараметрыАнализа.Вставить("СтруктураОбщиеМодули", СтруктураОбщихМодулейТекущейКонфигурации());
	АнализМодуля = Обработки.ОМ_ОформляторМодулей.АнализМодуля(ТекстМодуля, ПараметрыАнализа);
	
	Методы = Обработки.ОМ_ОформляторМодулей.СтруктураМетодовМодуля(СтрМодуль);
	Переменные = Обработки.ОМ_ОформляторМодулей.СтруктураПеременныхМодуля(СтрМодуль);
	
	Для каждого КлючЗначение Из Методы Цикл
		СтрМетод = ТаблицаМетоды.Добавить();
		Содержимое = КлючЗначение.Значение.Содержимое;
		ЗаполнитьЗначенияСвойств(СтрМетод, Содержимое);
		СтрМетод.Экспорт = Содержимое.Экспортная;
		СтрМетод.НомерКартинки = ?(Содержимое.ЭтоФункция, 1, 0);
	КонецЦикла;
	
	Для каждого КлючЗначение Из Переменные Цикл
		СтрПеременная = ТаблицаПеременные.Добавить();
		
		ИмяПеременной = КлючЗначение.Ключ;
		СтрПеременная.Имя = ИмяПеременной;
		
		Содержимое = КлючЗначение.Значение.Содержимое;
		ЗаполнитьЗначенияСвойств(СтрПеременная, Содержимое, "Контекст");
		НайденныеСтроки = Содержимое.ТаблицаПеременных.НайтиСтроки(Новый Структура("Имя", ИмяПеременной));
		Если ЗначениеЗаполнено(НайденныеСтроки) Тогда
			ЗаполнитьЗначенияСвойств(СтрПеременная, НайденныеСтроки[0], "Экспорт");
		КонецЕсли;
	КонецЦикла;
	
	ЗначениеВРеквизитФормы(АнализМодуля.ТаблицаВызовы, "ТаблицаВызовы");
	ЗначениеВРеквизитФормы(АнализМодуля.ТаблицаОбращенияКПолям, "ТаблицаИспользуемыеПоля");
	ЗначениеВРеквизитФормы(АнализМодуля.ТаблицаСоздаваемыеОбъекты, "ТаблицаСоздаваемыеОбъекты");
	
	КоличествоМетодов = ТаблицаМетоды.Количество();
	КоличествоПеременных = ТаблицаПеременные.Количество();
	КоличествоВызовы = ТаблицаВызовы.Количество();
	КоличествоИспользуемыеПоля = ТаблицаИспользуемыеПоля.Количество();
	КоличествоСоздаваемыеОбъекты = ТаблицаСоздаваемыеОбъекты.Количество();
КонецПроцедуры

&НаСервере
Функция СтруктураОбщихМодулейТекущейКонфигурации()
	Структ = Новый Структура;
	Для каждого ОМ Из Метаданные.ОбщиеМодули Цикл
		Структ.Вставить(ОМ.Имя, ОМ.ПолноеИмя());
	КонецЦикла;
	Возврат Структ;
КонецФункции

#КонецОбласти

