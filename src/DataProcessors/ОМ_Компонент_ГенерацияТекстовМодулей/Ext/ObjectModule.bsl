#Область ПрограммныйИнтерфейс

// Добавляет новую строку команды действий с предустановленными значениями идентификатора и представления.
// 
// Параметры:
//  КомандыДействий - ТаблицаЗначений - Таблица, в которую добавляется новая строка действия.
Процедура ДобавитьКомандыДействий(КомандыДействий) Экспорт
	НоваяСтрока = КомандыДействий.Добавить();
	НоваяСтрока.Представление = "Сформировать тексты модулей...";
	НоваяСтрока.Идентификатор = "ГенерацияТекстов";
	НоваяСтрока.Порядок = 1000;
	НоваяСтрока.СпособЗапускаДействия = "ОткрытиеФормы";
КонецПроцедуры

// Создает соответствие, связывающее элементы структуры с их текстами модулей
// 
// Параметры:
//  ДеревоСтруктуры - ДеревоЗначений - Дерево структуры модуля
// 
// Возвращаемое значение:
//  Соответствие - Строка дерева структуры (модуль) --> Текст модуля
Функция СоответствиеТекстыМодулей(ДеревоСтруктуры) Экспорт
	ПроверитьИнструкцииПрепроцессораПоДеревуСтруктуры(ДеревоСтруктуры);
	
	СоотвМодулей = Новый Соответствие;
	
	Для каждого СтрМодуль Из ДеревоСтруктуры.Строки Цикл
		ТекстМодуля = ТекстМодуля(СтрМодуль);
		СоотвМодулей.Вставить(СтрМодуль, ТекстМодуля);
	КонецЦикла;
	
	Возврат СоотвМодулей;
КонецФункции

// Возвращает сгенерированный текст кода для указанного элемента дерева структуры (модуля)
// 
// Параметры:
//  СтрЭлементМодуль - СтрокаДереваЗначений - Элемент модуля, содержащий структурное описание модуля для генерации текста
// 
// Возвращаемое значение:
//  Строка - Сгенерированный текст модуля
Функция ТекстМодуля(СтрЭлементМодуль) Экспорт
	Возврат ОбработкаГенерацииТекста().ТекстМодуля(СтрЭлементМодуль);
КонецФункции

// Генерирует текст метода на основе его структурного описания с возможностью настройки включения кода, документации и формата вывода
// 
// Параметры:
//  СтрЭлементМетод      - СтрокаДереваЗначений - Элемент дерева структуры типа Процедура или Функция
//  ВключатьКод          - Булево               - Признак необходимости включения тела метода в результат (по умолчанию Истина)
//  ВключатьДокументацию - Булево               - Признак необходимости включения документации метода в результат (по умолчанию Истина)
//  ТолькоСигнатура      - Булево               - Признак генерации только сигнатуры метода без тела (по умолчанию Ложь)
// 
// Возвращаемое значение:
//  Строка - Сгенерированный текст метода в соответствии с заданными параметрами
Функция ГенерироватьТекстМетода(СтрЭлементМетод, ВключатьКод = Истина, ВключатьДокументацию = Истина, ТолькоСигнатура = Ложь) Экспорт
	Возврат ОбработкаГенерацииТекста().ГенерироватьТекстМетода(СтрЭлементМетод, ВключатьКод, ВключатьДокументацию, ТолькоСигнатура);
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ОбработкаГенерацииТекста()
	Возврат Обработки.ОМ_Компонент_ГенерацияТекстовМодулей.Создать();
КонецФункции

Процедура ПроверитьИнструкцииПрепроцессораПоДеревуСтруктуры(ДеревоСтруктуры)
	Для каждого СтрМодуль Из ДеревоСтруктуры.Строки Цикл
		ПроверитьИнструкцииПрепроцессора(СтрМодуль);
	КонецЦикла;
КонецПроцедуры

Процедура ПроверитьИнструкцииПрепроцессора(СтрЭлемент)
	Если СтрЭлемент.Содержимое.Свойство("ИнструкцииПрепроцессора") Тогда
		
		Если Не ИнструкцииПрепроцессораКорректные(СтрЭлемент.Содержимое.ИнструкцииПрепроцессора) Тогда
			
			ТекстИсключения = "У элемента " + СтрЭлемент.Содержимое.Имя + " указаны некорректные инструкции препроцессора.";
			ВызватьИсключение ТекстИсключения;
	
		КонецЕсли; 
		
	КонецЕсли;
	
	Для каждого СтрВложенныйЭлемент Из СтрЭлемент.Строки Цикл
		ПроверитьИнструкцииПрепроцессора(СтрВложенныйЭлемент)	
	КонецЦикла;
КонецПроцедуры

Функция ИнструкцииПрепроцессораКорректные(ИнструкцииПрепроцессора)
	ЕстьИнструкции = Ложь;
	Для каждого КлючИЗначение Из ИнструкцииПрепроцессора Цикл
		Если КлючИЗначение.Значение = Истина Тогда
			ЕстьИнструкции = Истина;
			Прервать;
		КонецЕсли; 		
	КонецЦикла; 
	
	Возврат ЕстьИнструкции;
КонецФункции

#КонецОбласти

